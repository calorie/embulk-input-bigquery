plugins {
    id "java"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.5.5"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.1.0"
description = "BigQuery input plugin is an Embulk plugin that loads records from BigQuery."

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.10.43"

    implementation("org.embulk:embulk-util-config:0.3.3") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.43.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.43.
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7.5" // backport security fixes.
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    implementation "javax.validation:validation-api:1.1.0.Final"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.embulk:embulk-core:0.10.43"
    testImplementation "org.embulk:embulk-junit4:0.10.43"
    testImplementation "org.embulk:embulk-deps:0.10.43"
}

embulkPlugin {
    mainClass = "org.embulk.input.bigquery.BigqueryInputPlugin"
    category = "input"
    type = "bigquery"
}

javadoc {
    options {
        locale = "en_US"
        encoding = "UTF-8"
    }
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

gem {
    authors = [ "Yu Shigetani" ]
    email = [ "s2g4t1n2@gmail.com" ]
    summary = "BigQuery input plugin for Embulk"
    homepage = "https://github.com/calorie/embulk-input-bigquery"
    licenses = [ "MIT" ]

    from rootProject.file("LICENSE")
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(Checkstyle) {
    reports {
        // Not to skip up-to-date checkstyles.
        outputs.upToDateWhen { false }
    }
}

checkstyle {
    toolVersion "9.3"
    configFile = file("${projectDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = true
}
